//
// helper methods and tasks associated with loading replacement properties
//

apply from: "${project.projectDir}${File.separator}..${File.separator}automation-utilities${File.separator}build${File.separator}build.os.gradle"

Properties _props = null

// Gets the persistant data directory (creates it if needed)
ext.getProjectDirectory = { ->
	String home = getGlobalFolder();
	String persistantDirectory = "${home}${File.separator}home_automation${File.separator}${project.name}${File.separator}"
	
	// Create if it doesn't exist (this may require admin)
	File dir = new File(persistantDirectory)
   if(!dir.exists()) {
      mkdir(persistantDirectory)
	}
	
	return persistantDirectory
}

// Helper method to load a properties file (if it exists) and overwrite the given properties object
ext.loadPropertiesFromFile = {propsFile,propertiesObj ->
   if(propsFile.exists()) {
      Properties newProperties = new Properties()
      propsFile.withInputStream {
         newProperties.load(it)
      }
      
      // Loop through the loaded properties, overwriting any values already existing
      newProperties.each{ prop ->
         propertiesObj[prop.key] = newProperties[prop.key]
      }
   }
   return propertiesObj
}

// Load all properties from the various replacement.properties files
// Order of precedence:
// $HOME/.home-automation/{project.name}/replacement.properties
// $HOME/.home-automation/replacement.properties
// {projectDir}/replacement.properties
// automation-utilities/replacement.properties
ext.loadProperties = {
   if(_props != null)
   {
      return _props
   }   

   println("Loading properties from the following areas:")
   // Load replacement.properties from the automation-utilities repo
   _props = new Properties()
   File repoReplacements = new File("${projectDir}${File.separator}..${File.separator}automation-utilities${File.separator}replacement.properties")
   println("\t${repoReplacements}")
   _props = loadPropertiesFromFile(repoReplacements,_props)
   // Load replacement.properties from the project repo and
   // replace any properties found in automation-utilities
   File projectReplacements = new File("${project.projectDir}${File.separator}replacement.properties")
   println("\t${projectReplacements}")
   _props = loadPropertiesFromFile(projectReplacements,_props)
   // Load replacement.properties from the global area
   def projDir = getProjectDirectory();
   File globalReplacements = new File("${projDir}${File.separator}..${File.separator}replacement.properties")
   println("\t${globalReplacements}")
   _props = loadPropertiesFromFile(globalReplacements,_props)
   // Load replacement.properties from the project-specific folder in the global area
   File globalProjReplacements = new File("${projDir}replacement.properties")
   println("\t${globalProjReplacements}")
   _props = loadPropertiesFromFile(globalProjReplacements,_props)   
   // Load replacement.properties from the user's home
   def home = System.getProperty("user.home")
   File homeReplacements = new File("${home}${File.separator}.home_automation${File.separator}replacement.properties")
   println("\t${homeReplacements}")
   _props = loadPropertiesFromFile(homeReplacements,_props)
   // Load replacement.properties from the project specific file in the user's home repo
   File projHomeReplacements = new File("${home}${File.separator}.home_automation${File.separator}${project.name}${File.separator}replacement.properties")
   println("\t${projHomeReplacements}")
   _props = loadPropertiesFromFile(projHomeReplacements,_props)

   println("Loaded Properties: ${_props}")
   return _props
}

// Parses any file in the output directory with *.template
// First looks in the automation-utilities repo for replacements,
// then in the project's repo, then in the user's home directory
// for a file called "replacement.properties"
// If found, replaces values found in that file with values from the loaded file
task parseTemplates << {
   def templateFiles = fileTree("${project.buildDir}") {
      include "**/*.template"
   }

   Properties props = loadProperties()
   
   //  Apply the replacement properties to the template files
   templateFiles.files.each { file->
      project.copy {
         from file
         into project.buildDir
         rename {"${file}" - '.template'}
         expand([
            props
         ])
      }
   }
}
